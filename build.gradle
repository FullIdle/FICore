buildscript {
    repositories {
        mavenLocal()
        mavenCentral()
        maven {
            allowInsecureProtocol = true
            url = 'http://localhost:31647/snapshots'
        }
    }
    dependencies {
        classpath 'net.minecraftforge.gradle:ForgeGradle:6.0.+'
    }
}

plugins {
    id 'architectury-plugin' version '3.4-SNAPSHOT' apply(false)
    id 'dev.architectury.loom' version '1.11-SNAPSHOT' apply(false)
    id 'org.jetbrains.kotlin.jvm' version '2.2.0'
    id 'java'
    id 'maven-publish'
}

group = 'me.fullidle.ficore.ficore'
version = '1.3.7-SNAPSHOT'

def common = project(':common')

allprojects {
    apply plugin: 'java'
    apply plugin: 'org.jetbrains.kotlin.jvm'

    repositories {
        maven{url "https://maven.impactdev.net/repository/development/"}
        maven{url "https://dl.cloudsmith.io/public/geckolib3/geckolib/maven/"}
        maven{url "https://thedarkcolour.github.io/KotlinForForge/"}

        maven {url 'https://maven.neoforged.net/releases'}
        maven {
            allowInsecureProtocol = true
            url "http://localhost:31647/snapshots"
        }
        maven { url 'https://oss.sonatype.org/content/repositories/snapshots' }
        mavenCentral()
        mavenLocal()
    }

    dependencies {
        if (project != common && project != rootProject) {
            compileOnly common
        }
        /*==>lombok<==*/
        compileOnly('org.projectlombok:lombok:1.18.34')
        annotationProcessor('org.projectlombok:lombok:1.18.34')

        compileOnly "org.jetbrains.kotlin:kotlin-reflect:2.2.0"

        compileOnly 'me.clip:placeholderapi:2.10.3'
    }

    tasks.withType(JavaCompile).configureEach {
        options.encoding = "UTF-8"
    }

    processResources {
        setFilteringCharset("UTF-8")
        expand project.properties
    }
}

publishing {
    publications {
        mavenJava(MavenPublication) {
            groupId = project.group
            artifactId = project.name
            version = project.version
            from components.java
        }
    }
    repositories {
        mavenLocal()
        maven {
            allowInsecureProtocol = true
            url = uri(fistudioUrl)
            credentials {
                username = fistudioUsername
                password = fistudioPassword
            }
        }
    }
}

def project21 = project(":v1_21")

dependencies {
    compileOnly 'org.spigotmc:spigot:1.16.5-R0.1-SNAPSHOT'
    compileOnly 'org.spigotmc:spigot-api:1.16.5-R0.1-SNAPSHOT'
    compileOnly 'net.md-5:bungeecord-api:1.19-R0.1-SNAPSHOT'

    compileOnly subprojects.findAll {
        it != project21 && !project21.subprojects.contains(it)
    }
}

jar {
    archiveFileName = "${project.name}-${project.version}.jar"
    from(subprojects.collect { it.sourceSets.main.output })
}

java {
    sourceCompatibility = JavaVersion.VERSION_1_8
    targetCompatibility = JavaVersion.VERSION_1_8
}
