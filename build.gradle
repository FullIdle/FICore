buildscript {
    repositories {
        mavenLocal()
        mavenCentral()
        maven {url = 'https://repo.figsq.top/snapshots'}
    }
    dependencies {
        classpath 'net.minecraftforge.gradle:ForgeGradle:6.0.+'
    }
}

plugins {
    id 'architectury-plugin' version '3.4-SNAPSHOT' apply(false)
    id 'dev.architectury.loom' version '1.11-SNAPSHOT' apply(false)
    id 'org.jetbrains.kotlin.jvm' version '2.2.0'
    id 'com.gradleup.shadow' version '8.3.6' apply false
    id 'java'
    id 'maven-publish'
}

group = 'me.fullidle.ficore.ficore'
version = '1.4.1-STABLE'

def common = project(':common')

allprojects {
    apply plugin: 'java'
    apply plugin: 'org.jetbrains.kotlin.jvm'

    repositories {
        mavenCentral()
        mavenLocal()
        maven { url "https://maven.architectury.dev/" }
        maven {url 'https://maven.neoforged.net/releases'}
        maven {url "https://artefacts.cobblemon.com/releases"}
        maven{url "https://maven.impactdev.net/repository/development/"}
        maven { url = "https://hub.spigotmc.org/nexus/content/repositories/snapshots/" }
        maven { url 'https://oss.sonatype.org/content/repositories/snapshots' }
        maven { url = "https://repo.papermc.io/repository/maven-public/" }
        maven {url "https://repo.extendedclip.com/releases"}
        maven{url "https://dl.cloudsmith.io/public/geckolib3/geckolib/maven/"}
        maven{url "https://thedarkcolour.github.io/KotlinForForge/"}
        maven {url = 'https://repo.figsq.top/snapshots'}
    }

    dependencies {
        if (project != common && project != rootProject) {
            compileOnly common
        }
        /*==>lombok<==*/
        compileOnly('org.projectlombok:lombok:1.18.34')
        annotationProcessor('org.projectlombok:lombok:1.18.34')

        compileOnly "org.jetbrains.kotlin:kotlin-reflect:2.2.0"

        compileOnly "me.clip:placeholderapi:2.11.7"
    }

    tasks.withType(JavaCompile).configureEach {
        options.encoding = "UTF-8"
    }

    processResources {
        setFilteringCharset("UTF-8")
        expand project.properties
    }
}

publishing {
    publications {
        mavenJava(MavenPublication) {
            groupId = project.group
            artifactId = project.name
            version = project.version
            from components.java
        }
    }
    repositories {
        maven {url = 'https://hub.spigotmc.org/nexus/content/groups/public/' }
        maven { url = "https://repo.papermc.io/repository/maven-public/" }
        mavenLocal()
//        maven {
//            url = uri(fistudioUrl)
//            credentials {
//                username = fistudioUsername
//                password = fistudioPassword
//            }
//        }
    }
}

def project21 = project(":v1_21")

dependencies {
    compileOnly 'org.spigotmc:spigot:1.16.5-R0.1-SNAPSHOT'
    compileOnly 'org.spigotmc:spigot-api:1.16.5-R0.1-SNAPSHOT'
    // https://mvnrepository.com/artifact/net.md-5/bungeecord-api
    compileOnly 'net.md-5:bungeecord-api:1.21-R0.1'

    //排除1.21.1版本相关的内容
    compileOnly subprojects.findAll {
        it != project21 && !project21.subprojects.contains(it)
    }
}

def needFullPlatform = [
        'Cob'
]

jar {
    archiveFileName = "${project.name}-${project.version}.jar"
    gradle.afterProject { p ->
        if (p.name in needFullPlatform) {
            println p.toString() + "testtest"
            from p.zipTree(p.tasks.named("fullPlatformJar").get().archiveFile)
        } else {
            from p.sourceSets.main.output
        }
    }
}
java {
    sourceCompatibility = JavaVersion.VERSION_1_8
    targetCompatibility = JavaVersion.VERSION_1_8
}
